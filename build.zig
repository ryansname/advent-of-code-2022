const fs = std.fs;
const std = @import("std");

pub fn build(b: *std.build.Builder) void {
    // Standard target options allows the person running `zig build` to choose
    // what target to build for. Here we do not override the defaults, which
    // means any target is allowed, and the default is native. Other options
    // for restricting supported target set are available.
    const target = b.standardTargetOptions(.{});

    // Standard release options allow the person running `zig build` to select
    // between Debug, ReleaseSafe, ReleaseFast, and ReleaseSmall.
    const mode = b.standardReleaseOptions();
    const test_step = b.step("test", "Run unit tests");
    const test_all_step = b.step("test-all", "Run all unit tests");
    var latest_test_step: ?*std.build.LibExeObjStep = null;

    const run_step = b.step("run", "Run the latest modified puzzle");
    const run_all_step = b.step("run-all", "Run all puzzles");
    var latest_run_step: ?*std.build.RunStep = null;

    var cwd = fs.cwd();
    var root = cwd.openDir(b.build_root, .{}) catch unreachable;

    var latest_mtime: i128 = 0;
    comptime var i: u8 = 1;
    inline while (i <= 25) : (i += 1) {
        const i_string = b.fmt("{:0>2}", .{i});
        const source_file = b.fmt("src/day{s}.zig", .{i_string});

        const day_stat = root.statFile(source_file);

        if (day_stat) |stat| {
            const day_build = addSingleDay(b, target, mode, i_string, source_file);
            test_all_step.dependOn(&day_build.exe_tests.step);
            run_all_step.dependOn(&day_build.run_cmd.step);

            if (stat.mtime > latest_mtime) {
                latest_run_step = day_build.run_cmd;
                latest_test_step = day_build.exe_tests;
                latest_mtime = stat.mtime;
            }
        } else |err| {
            if (err != error.FileNotFound) {
                std.debug.panic("Error opening file {s}: {}", .{ source_file, err });
            }
        }
    }
    run_step.dependOn(&latest_run_step.?.step);
    test_step.dependOn(&latest_test_step.?.step);
}

fn addSingleDay(
    b: *std.build.Builder,
    target: std.zig.CrossTarget,
    mode: std.builtin.Mode,
    i_string: []const u8,
    source_file: []const u8,
) struct {
    exe_tests: *std.build.LibExeObjStep,
    run_cmd: *std.build.RunStep,
} {
    const exe = b.addExecutable(b.fmt("advent-of-code-2022-day-{s}", .{i_string}), source_file);
    exe.setTarget(target);
    exe.setBuildMode(mode);
    exe.install();

    const run_cmd = exe.run();
    run_cmd.step.dependOn(b.getInstallStep());
    if (b.args) |args| {
        run_cmd.addArgs(args);
    }

    const run_step = b.step(b.fmt("run-{s}", .{i_string}), b.fmt("Run the puzzle from day {s}", .{i_string}));
    run_step.dependOn(&run_cmd.step);

    const exe_tests = b.addTest(source_file);
    exe_tests.setTarget(target);
    exe_tests.setBuildMode(mode);

    return .{
        .exe_tests = exe_tests,
        .run_cmd = run_cmd,
    };
}
